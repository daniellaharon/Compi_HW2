%{
#include "parser.tab.hpp"
#include "output.hpp"
#include "iostream"
using namespace output;

void yyerror(const char*);
extern int yylex();
extern int yylineno;
%}

%token VOID INT BYTE B BOOL TRUE FALSE RETURN IF WHILE BREAK CONTINUE SC
COMMA ID NUM STRING

%right ASSIGN
%left OR
%left AND
%left EQOP
%left RELOP
%left BINOP
%left MULTDIVTOP
%right NOT
%left RPAREN LPAREN LBRACE RBRACE
%right ELSE

%%
Prog: Funcs                                     {printProductionRule(1);}
;
Funcs: /*epsilon*/                              {printProductionRule(2);}
        | FuncDecl Funcs                        {printProductionRule(3);}
 ;
FuncDecl: RetType ID LPAREN Formals RPAREN LBRACE Statements RBRACE {printProductionRule(4);}
;
RetType: Type                                   {printProductionRule(5);}
         | VOID                                 {printProductionRule(6);}
;
Formals: /*epsilon*/                            {printProductionRule(7);}
        | FormalsList                           {printProductionRule(8);}
;
FormalsList: FormalDecl                         {printProductionRule(9);}
             | FormalDecl COMMA FormalsList     {printProductionRule(10);}
;
FormalDecl: Type ID                             {printProductionRule(11);}
;
Statements: Statement                           {printProductionRule(12);}
            | Statements Statement              {printProductionRule(13);}
;
Statement: LBRACE Statements RBRACE             {printProductionRule(14);}
            | Type ID SC                        {printProductionRule(15);}
            | Type ID ASSIGN Exp SC             {printProductionRule(16);}
            | ID ASSIGN Exp SC                  {printProductionRule(17);}
            | Call SC                           {printProductionRule(18);}
            | RETURN SC                         {printProductionRule(19);}
            | RETURN Exp SC                     {printProductionRule(20);}
            | IF LPAREN Exp RPAREN Statement    {printProductionRule(21);}
            | IF LPAREN Exp RPAREN Statement ELSE Statement   {printProductionRule(22);}
            | WHILE LPAREN Exp RPAREN Statement {printProductionRule(23);}
            | BREAK SC                          {printProductionRule(24);}
            | CONTINUE SC                       {printProductionRule(25);}
;
Call :ID LPAREN ExpList RPAREN                  {printProductionRule(26);}
      | ID LPAREN RPAREN                        {printProductionRule(27);}
;
ExpList: Exp                                     {printProductionRule(28);}
         | Exp COMMA ExpList                     {printProductionRule(29);}
;
Type :INT                                        {printProductionRule(30);}
      | BYTE                                     {printProductionRule(31);}
      | BOOL                                     {printProductionRule(32);}
;
Exp: LPAREN Exp RPAREN                           {printProductionRule(33);}
      | Exp BINOP Exp                            {printProductionRule(34);}
      | Exp MULTDIVTOP Exp                       {printProductionRule(34);}
      | ID                                       {printProductionRule(35);}
      | Call                                     {printProductionRule(36);}
      | NUM                                      {printProductionRule(37);}
      | NUM B                                    {printProductionRule(38);}
      | STRING                                   {printProductionRule(39);}
      | TRUE                                     {printProductionRule(40);}
      | FALSE                                    {printProductionRule(41);}
      | NOT Exp                                  {printProductionRule(42);}
      | Exp AND Exp                              {printProductionRule(43);}
      | Exp OR Exp                               {printProductionRule(44);}
      | Exp RELOP Exp                            {printProductionRule(45);}
      | Exp EQOP Exp                             {printProductionRule(45);}
      | LPAREN Type RPAREN Exp                   {printProductionRule(46);}
;
%%
void yyerror(const char*) {errorSyn(yylineno); exit(1);}
int main() {
    return yyparse();
}